*
*	Ｍｓｘ Ｓｉｍｕｌａｔｅｒ
*
*		[[[ Ｉ／Ｏ Ｓｉｍｕｌａｔｅｒ ]]]		
*
*				1995.9.15		by Kuni.
*

	.include	iocscall.equ
*	.include	doscall.equ

	.xdef	r_port_90
	.xdef	r_port_A9
	.xdef	r_port_AA
	.xdef	r_port_AB
	.xdef	r_port_B4
	.xdef	r_port_B5
	.xdef	r_port_FC
	.xdef	r_port_FD
	.xdef	r_port_FE
	.xdef	r_port_FF

	.xdef	w_port_90
	.xdef	w_port_91
	.xdef	w_port_AA
	.xdef	w_port_AB
	.xdef	w_port_B4
	.xdef	w_port_B5

	.xdef	p_FC
	.xdef	p_FD
	.xdef	p_FE
	.xdef	p_FF

	.xref	slot_reg

	.xdef	_KEYSNS_tbl_ptr

	.xref	cpu_emu_yield
	.xref	KEY_vector
	.xref	debug_log_level

	.68000

USART_data	equ	$e8802f		* USART データポート

	.text
	.even

RTC68	equ	$e8a001

********************************************************************
*
*	ＩＮＰＵＴ
*		d0.b に読み込んだ値を入れて rts
*
********************************************************************
r_port_90:				* プリンタ ＢＵＳＹ検出
	move.b	$E9C001,d0		* Inside p375
	andi.b	#%00100000,d0
	lsr.b	#4,d0
	rts

r_port_A9:
	move.b	p_AA,d0
	andi.w	#$000f,d0
	lea	_KEYSNS_tbl,a0
	move.b	(a0,d0.w),d0
	rts

	.data

_KEYSNS_tbl:
	.dc.b	$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff
_KEYSNS_tbl_ptr:
	.dc.l	_KEYSNS_tbl

	.text

r_port_AA:
	move.b	p_AA,d0
	rts

r_port_AB:				* PPI Control Register
	move.b	p_AB,d0
	rts

*
*	Ｒｅａｌ Ｔｉｍｅ Ｃｌｏｃｋ
*
r_port_B4:
	move.b	p_B4,d0
	rts

r_port_B5:				* Real Time Clock
	move.b	p_B4,d0			* d0 = register number
	cmpi.b	#13,d0
	beq	read_RTC_mode
	bmi	read_RTC_reg
	cmpi.b	#15,d0
	beq	@f
	move.b	RTCR_14,d0
	rts
@@:	move.b	RTC68+15*2,d0
	rts

read_RTC_mode:
	move.b	RTC68+13*2,d0
	andi.b	#%1000,d0
	move.b	RTCR_13,d1
	andi.b	#%0111,d1
	or.b	d1,d0
	rts

read_RTC_reg:
	andi.w	#$000f,d0		* 上位バイトをクリア
	move.b	RTCR_13,d3
	andi.w	#%00000011,d3
	beq	r_block0
	bclr.l	#1,d3
	bne	r_block2or3

	lea.l	RTC1,a0			* ブロック１の時
	add.w	d0,d0
	move.b	0(a0,d0.w),d0
	rts

r_block0:
	lea.l	RTC68,a0
	add.w	d0,d0
	move.b	0(a0,d0.w),d0
	rts

r_block2or3:
	move.w	d3,d3
	bne	block3
	lea.l	RTC2,a0
	move.b	0(a0,d0.w),d0
	rts
block3:
	lea.l	RTC3,a0
	move.b	0(a0,d0.w),d0
	rts


r_port_FC:
	move.b	p_FC,d0
	rts
r_port_FD:
	move.b	p_FD,d0
	rts
r_port_FE:
	move.b	p_FE,d0
	rts
r_port_FF:
	move.b	p_FF,d0
	rts


****************************************************************************
*
*	ＯＵＴＰＵＴ
*		d1.b に書き込む値が入ってくる
*
*	d0〜d3 を使ってよい
*
****************************************************************************
w_port_90:				* プリンタポート
	rts
	eori.b	#1,d1
	move.b	d1,$E8C003		* プリンタ STROBE	(Inside p373)
	rts
w_port_91:				* プリンタポート
	rts
	move.b	d1,$E8C001		* プリンタ DATA		(Inside p373）	
	rts

w_port_AA:
	move.b	p_AA,d0			* 以前の値を読み込む
	move.b	d1,p_AA

	eor.b	d1,d0
	btst.l	#6,d0			* ビット６は CAPS ランプの制御
	beq	@f			* 変化していなければ無視
	btst.l	#6,d1			* 書き込んだ値を見て点灯／消灯を決める
	beq	1f			* 	0 ・・ on   1 ・・ off
	moveq.l	#3,d1			* CAPS
	clr.b	d2			* 消灯
	IOCS	_LEDMOD
	bra	@f
1:
	moveq.l	#3,d1			* CAPS
	moveq.l	#1,d2			* 点灯
	IOCS	_LEDMOD
@@:
	rts

*
*	port AB 解析結果
*
*	　負の値を書き込むとそれが port AB の値になる。つまり port AB を読むとその
*	値が返ってくる。（この値がに使われるのかは？＜−どうも各ポートのＩ／Ｏの方
*	向の設定に使うらしい。MS.Xでは関係ない）
*
*	　正の値を書き込んだ時、
*		偶数の時：port AA の指定されたビットを０にする。ビットの指定はビット
*			  1,2,3 の組み合わせによる。
*		奇数の時：port AA の指定されたビットを１にする。ビットの指定はビット
*			  1,2,3 の組み合わせによる。
*
w_port_AB:
	move.b	d1,d0
	bpl	@f
	move.b	d0,p_AB
	rts
@@:	move.b	p_AA,d1
	andi.b	#%00001111,d0
	lsr.b	#1,d0
	bcc	@f
	bset.l	d0,d1			* 奇数の時
	bra	w_port_AA
@@:	bclr.l	d0,d1			* 偶数の時
	bra	w_port_AA


*
*	Ｒｅａｌ Ｔｉｍｅ Ｃｌｏｃｋ
*
w_port_B4:
	andi.b	#%00001111,d1
	move.b	d1,p_B4
	rts

w_port_B5:
	move.b	p_B4,d0
	cmpi.b	#13,d0
	beq	set_RTC_mode
	bmi	write_RTC_reg
	cmpi.b	#15,d0
	beq	@f
	move.b	d1,RTCR_14
	rts
@@:	move.b	d1,RTC68+15*2
	rts

set_RTC_mode:
	move.b	d1,RTCR_13
	move.b	RTC68+13*2,d0		* 68 の RTC13 を読み込み
	andi.b	#%0100,d0
	andi.b	#%1001,d1
	or.b	d1,d0
	move.b	d0,RTC68+13*2
	rts	

write_RTC_reg:
	rts				* RTCの書き換えは怖いので無効化

	andi.w	#$000f,d0		* 上位バイトをクリア
	move.b	RTCR_13,d3
	andi.w	#%00000011,d3
	beq	w_block0
	bclr.l	#1,d3
	bne	w_block2or3

	lea.l	RTC1mask,a0		* ブロック１の時
	and.b	0(a0,d0.w),d1
	lea.l	RTC1,a0
	add.w	d0,d0
	move.b	d1,0(a0,d0.w)
	rts

w_block0:
	lea.l	RTC68,a0
	move.b	13*2(a0),d2		* ６８のＲＴＣモードが変わってるかもしれない
	andi.b	#%1100,d2		* から、設定し直す
	move.b	d2,13*2(a0)
	add.w	d0,d0
	move.b	d1,0(a0,d0.w)
	rts

w_block2or3:
	move.w	d3,d3
	bne	w_block3	
	lea.l	RTC2,a0
	move.b	d1,0(a0,d0.w)
	rts

w_block3:
	lea.l	RTC3,a0
	move.b	d1,0(a0,d0.w)
	rts

	.data

RTC1:	.dc.b	0		* 0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0		* 12

RTC2:	.dc.b	0x0a		* 0
	.dc.b	0
	.dc.b	0
	.dc.b	1
	.dc.b	0
	.dc.b	2
	.dc.b	15
	.dc.b	4
	.dc.b	7
	.dc.b	1
	.dc.b	0
	.dc.b	0
	.dc.b	0		* 12

RTC3:	.dc.b	0		* 0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0
	.dc.b	0		* 12

RTCR_13:
	.dc.b	%1000
RTCR_14:
	.dc.b	%0000

RTC1mask:
	.dc.b	0
	.dc.b	0
	.dc.b	15
	.dc.b	7
	.dc.b	15
	.dc.b	3
	.dc.b	7
	.dc.b	15
	.dc.b	3
	.dc.b	0
	.dc.b	1
	.dc.b	3
	.dc.b	0

	.data

*	Ｉ／Ｏポートの値保存場所
p_AA:	.dc.b	0
p_AB:	.dc.b	0			* PPI Control Register

p_B4:	.dc.b	0			* RTC Register

p_FC:	.dc.b	3			* マッパースロット選択レジスター
p_FD:	.dc.b	2			* 初期値に意味はない。何となく。
p_FE:	.dc.b	1
p_FF:	.dc.b	0

	.end